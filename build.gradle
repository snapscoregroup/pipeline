group 'com.snapscore'
version "${JAR_VERSION}"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:5.0.0'

        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"

        classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"

    }
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 17.0
targetCompatibility = 17.0

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def vertxVersion = '4.3.2'

dependencies {

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    api group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.14.9'

    api "io.projectreactor.netty:reactor-netty:1.0.21"
    api "io.projectreactor:reactor-core:3.4.21"

    testImplementation("io.projectreactor:reactor-test:3.4.21")
    testImplementation 'io.projectreactor.tools:blockhound:1.0.6.RELEASE'

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'

    // https://mvnrepository.com/artifact/org.mockito/mockito-all
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.6.1'

    // Vertx LIBS
    api group: 'io.vertx', name: 'vertx-core', version: "${vertxVersion}"
    testImplementation group: 'io.vertx', name: 'vertx-unit', version: "${vertxVersion}"

    // logback
    api group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.11'
    api group: 'ch.qos.logback', name: 'logback-core', version: '1.2.11'
    api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'

    api group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'

    api 'org.apache.commons:commons-lang3:3.12.0'
    api group: 'org.apache.commons', name: 'commons-collections4', version: '4.3'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

shadowJar {
    baseName = project.name
    classifier = ''
    archiveVersion = version
}

jar {
    manifest {
        attributes 'Implementation-Title': 'pipeline',
                'Implementation-Version': version,
                'Main-Class': 'com.snapscore.pipeline'
    }
    classifier = 'core'
}

// Allows the Fat Jar to be uploaded to Artifactory
artifacts {
    archives shadowJar
    archives jar
}

configurations {
    fatJarRuntimeElements {
        canBeResolved = false
        canBeConsumed = true
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
            attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, LibraryElements.JAR))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EMBEDDED)) // means no relocation of packages
        }
        outgoing.artifact(shadowJar)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/snapscoregroup/pipeline")
            credentials(HttpHeaderCredentials) {
                name "Authorization"
                value "Bearer ${System.getenv("GITHUB_TOKEN")}"
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}
